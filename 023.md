rvierdiiev

medium

# DnGmxJuniorVault.maxDeposit and DnGmxJuniorVault.afterDeposit calculate maximum assets that are allowed to deposit in different ways

## Summary
DnGmxJuniorVault.maxDeposit and DnGmxJuniorVault.afterDeposit calculate maximum assets that are allowed to deposit in different ways.
## Vulnerability Detail
This is how DnGmxJuniorVault.maxDeposit function calculates max amount of assets that are allowed to deposit.
https://github.com/sherlock-audit/2022-10-rage-trade/blob/main/dn-gmx-vaults/contracts/vaults/DnGmxJuniorVault.sol#L531-L533
```solidity
    function maxDeposit(address) public view override(IERC4626, ERC4626Upgradeable) returns (uint256) {
        return state.depositCap - state.totalAssets(true);
    }
```

And this is how DnGmxJuniorVault.afterDeposit function calculates max amount of assets that are allowed to deposit.
https://github.com/sherlock-audit/2022-10-rage-trade/blob/main/dn-gmx-vaults/contracts/vaults/DnGmxJuniorVault.sol#L719-L729
```solidity
    function afterDeposit(
        uint256,
        uint256,
        address
    ) internal override {
        if (totalAssets() > state.depositCap) revert DepositCapExceeded();
        (uint256 currentBtc, uint256 currentEth) = state.getCurrentBorrows();


        //rebalance of hedge based on assets after deposit (after deposit assets)
        state.rebalanceHedge(currentBtc, currentEth, totalAssets(), false);
    }
```

As you can see in one case it uses `totalAssets()` function to get all assets.
```solidity
    function totalAssets() public view override(IERC4626, ERC4626Upgradeable) returns (uint256) {
        return state.totalAssets();
    }
```
And in another case it uses `state.totalAssets(true)`.

This is how `state.totalAssets()` and `state.totalAssets(true)` are handled in DnGmxJuniorVaultManager.
https://github.com/sherlock-audit/2022-10-rage-trade/blob/main/dn-gmx-vaults/contracts/libraries/DnGmxJuniorVaultManager.sol#L997-L1052
```solidity
    function totalAssets(State storage state) external view returns (uint256) {
        return _totalAssets(state, false);
    }


    ///@notice returns the total assets deposited to the vault (in glp amount)
    ///@param state set of all state variables of vault
    ///@param maximize true for maximizing the total assets value and false to minimize
    ///@return total asset amount (glp + usdc (in glp terms))
    function totalAssets(State storage state, bool maximize) external view returns (uint256) {
        return _totalAssets(state, maximize);
    }


    ///@notice returns the total assets deposited to the vault (in glp amount)
    ///@param state set of all state variables of vault
    ///@param maximize true for maximizing the total assets value and false to minimize
    ///@return total asset amount (glp + usdc (in glp terms))
    function _totalAssets(State storage state, bool maximize) private view returns (uint256) {
        // usdc deposited by junior tranche (can be negative)
        int256 dnUsdcDeposited = state.dnUsdcDeposited;


        // convert int into two uints basis the sign
        uint256 dnUsdcDepositedPos = dnUsdcDeposited > int256(0) ? uint256(dnUsdcDeposited) : 0;
        uint256 dnUsdcDepositedNeg = dnUsdcDeposited < int256(0) ? uint256(-dnUsdcDeposited) : 0;


        // add positive part to unhedgedGlp which will be added at the end
        // convert usdc amount into glp amount
        uint256 unhedgedGlp = (state.unhedgedGlpInUsdc + dnUsdcDepositedPos).mulDivDown(
            PRICE_PRECISION,
            _getGlpPrice(state, !maximize)
        );


        // calculate current borrow amounts
        (uint256 currentBtc, uint256 currentEth) = _getCurrentBorrows(state);
        uint256 totalCurrentBorrowValue = _getBorrowValue(state, currentBtc, currentEth);


        // add negative part to current borrow value which will be subtracted at the end
        // convert usdc amount into glp amount
        uint256 borrowValueGlp = (totalCurrentBorrowValue + dnUsdcDepositedNeg).mulDivDown(
            PRICE_PRECISION,
            _getGlpPrice(state, !maximize)
        );


        // if we need to minimize then add additional slippage
        if (!maximize) unhedgedGlp = unhedgedGlp.mulDivDown(MAX_BPS - state.slippageThresholdGmxBps, MAX_BPS);
        if (!maximize) borrowValueGlp = borrowValueGlp.mulDivDown(MAX_BPS - state.slippageThresholdGmxBps, MAX_BPS);


        // total assets considers 3 parts
        // part1: glp balance in vault
        // part2: glp balance in batching manager
        // part3: pnl on AAVE (usdc deposit by junior tranche (i.e. dnUsdcDeposited) - current borrow value)
        return
            state.fsGlp.balanceOf(address(this)) +
            state.batchingManager.dnGmxJuniorVaultGlpBalance() +
            unhedgedGlp -
            borrowValueGlp;
    }
```

That means that one of DnGmxJuniorVault.maxDeposit and DnGmxJuniorVault.afterDeposit functions uses incorrect amount of assets.
## Impact
Incorrect calculations because of incorrect assets value.
## Code Snippet
Provided above.
## Tool used

Manual Review

## Recommendation
Use same approach to get total assets in both functions.