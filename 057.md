ctf_sec

medium

# GMX LP (GLP) token price is vulnerable to manipulation.

## Summary

GMX LP token (GLP)  price is vulnerable to manipulation 

## Vulnerability Detail

Let us look into how the GLP token price is determined:

```solidity
///@notice returns the price of glp token
///@param state set of all state variables of vault
///@param maximize true to get maximum price and flase to get minimum
///@return glp price in usd
function getGlpPrice(State storage state, bool maximize) external view returns (uint256) {
    return _getGlpPrice(state, maximize);
}

///@notice returns the price of glp token
///@param state set of all state variables of vault
///@param maximize true to get maximum price and flase to get minimum
///@return glp price in usd
function _getGlpPrice(State storage state, bool maximize) private view returns (uint256) {
    uint256 aum = state.glpManager.getAum(maximize);
    uint256 totalSupply = state.glp.totalSupply();

    // price per glp token = (total AUM / total supply)
    return aum.mulDivDown(PRICE_PRECISION, totalSupply * 1e24);
}
```

the GMX LP token price is determined by the total pool asset value in GMX / LP token totalSupply. While there is nothing wrong about this equal and implementation, using the spot price of the LP token like this is  still very vulnerable to manipulation.

Let us look into what is state.glpManager.getAum doing:

https://github.com/gmx-io/gmx-contracts/blob/71f9a57319e63087ff225374f306c7b82928a0e9/contracts/core/GlpManager.sol#L136

```solidity
function getAum(bool maximise) public view returns (uint256) {
	uint256 length = vault.allWhitelistedTokensLength();
	uint256 aum = aumAddition;
	uint256 shortProfits = 0;
	IVault _vault = vault;

	for (uint256 i = 0; i < length; i++) {
		address token = vault.allWhitelistedTokens(i);
		bool isWhitelisted = vault.whitelistedTokens(token);

		if (!isWhitelisted) {
			continue;
		}

		uint256 price = maximise ? _vault.getMaxPrice(token) : _vault.getMinPrice(token);
		uint256 poolAmount = _vault.poolAmounts(token);
		uint256 decimals = _vault.tokenDecimals(token);

		if (_vault.stableTokens(token)) {
			aum = aum.add(poolAmount.mul(price).div(10 ** decimals));
		} else {
			// add global short profit / loss
			uint256 size = _vault.globalShortSizes(token);

			if (size > 0) {
				(uint256 delta, bool hasProfit) = getGlobalShortDelta(token, price, size);
				if (!hasProfit) {
					// add losses from shorts
					aum = aum.add(delta);
				} else {
					shortProfits = shortProfits.add(delta);
				}
			}

			aum = aum.add(_vault.guaranteedUsd(token));

			uint256 reservedAmount = _vault.reservedAmounts(token);
			aum = aum.add(poolAmount.sub(reservedAmount).mul(price).div(10 ** decimals));
		}
	}

	aum = shortProfits > aum ? 0 : aum.sub(shortProfits);
	return aumDeduction > aum ? 0 : aum.sub(aumDeduction);
}
```

We see there is whitelist implementation: if the token is not whitelisted in the vault, the token does not count towards the total asset price liqudity.

And there is possibilty that this function returns 0

```solidity
	aum = shortProfits > aum ? 0 : aum.sub(shortProfits);
	return aumDeduction > aum ? 0 : aum.sub(aumDeduction);
```

The vault on GMX can basically add or remove whitelist token, which greatly affecting the total asset price.

Adding token to the whitelist inflates the aum = state.glpManager.getAum(maximize);which inflate the GMX Lp token price used in the rage trade.

Removing token to the whitelist inflates aum = state.glpManager.getAum(maximize);which deflate the GMX Lp token price used in the rage trade.

GMX Lp token price is also affecting suddenly adding liqudity and minting a large of token or remove liqudity and burn a large amount of token.

Also when does it return to 0?

```solidity
	aum = shortProfits > aum ? 0 : aum.sub(shortProfits);
	return aumDeduction > aum ? 0 : aum.sub(aumDeduction);
```

What is aumDeduction? this value can by set by admin in GMX side, with no timelock:

https://github.com/gmx-io/gmx-contracts/blob/71f9a57319e63087ff225374f306c7b82928a0e9/contracts/core/GlpManager.sol#L93

```solidity
function setAumAdjustment(uint256 _aumAddition, uint256 _aumDeduction) external onlyGov {
	aumAddition = _aumAddition;
	aumDeduction = _aumDeduction;
}
```

## Impact

Basically, user can manipulate the GLP price by adding or removing liqudity. GMX admin governoance can changing the aumDeduction parameter to the GLP price to 0, or adding whitelisted token to inflate the GLP price or removing whitelisted token to deflate the GLP price.

Either GLP price is inflated, or deflated, the impact is not good on rage trading.

Let us say the price is deflated and GLP price is 0.

We get division by 0 error in a few places:

in DnGmxJuniorVaultManager.sol#_convertAssetToAUsdc

```solidity
function _convertAssetToAUsdc(State storage state, uint256 usdcAmountDesired)
	internal
	returns (uint256 usdcAmountOut)
{
```

which calls:

```solidity
uint256 glpAmountInput = usdcAmountDesired.mulDivDown(PRICE_PRECISION, _getGlpPrice(state, false));
```

and in DnGmxJuniorVaultManager.sol#totalAsset

```solidity
///@notice returns the total assets deposited to the vault (in glp amount)
///@param state set of all state variables of vault
///@param maximize true for maximizing the total assets value and false to minimize
///@return total asset amount (glp + usdc (in glp terms))
function _totalAssets(State storage state, bool maximize) private view returns (uint256) {
```

which calls:

```solidity
uint256 unhedgedGlp = (state.unhedgedGlpInUsdc + dnUsdcDepositedPos).mulDivDown(
	PRICE_PRECISION,
	_getGlpPrice(state, !maximize)
);

// calculate current borrow amounts
(uint256 currentBtc, uint256 currentEth) = _getCurrentBorrows(state);
uint256 totalCurrentBorrowValue = _getBorrowValue(state, currentBtc, currentEth);

// add negative part to current borrow value which will be subtracted at the end
// convert usdc amount into glp amount
uint256 borrowValueGlp = (totalCurrentBorrowValue + dnUsdcDepositedNeg).mulDivDown(
	PRICE_PRECISION,
	_getGlpPrice(state, !maximize)
);
```

## Code Snippet

https://github.com/sherlock-audit/2022-10-rage-trade/blob/main/dn-gmx-vaults/contracts/libraries/DnGmxJuniorVaultManager.sol#L1008-L1053

## Tool used

Manual Review

## Recommendation

Not use the spot price of the GMX token Lp price, can take a snapshot of the GLP price. or use TWAP price of the token. The project also need to gracefully handle the case when GLP token is 0 to resolve the division by zero error.