ctf_sec

medium

# Stale oracle price can be used because the oracle source is lack of price refreshness check.

## Summary

Stale oracle price can be used.

## Vulnerability Detail

The code currently uses Aave's oracle price and Aave use chainlink oracle data.

```solidity
5 results - 2 files

dn-gmx-vaults\contracts\libraries\DnGmxJuniorVaultManager.sol:
  1102          // AAVE oracle
  1103:         uint256 price = state.oracle.getAssetPrice(address(token));
  1104  

  1150          uint256 decimals = token.decimals();
  1151:         uint256 price = state.oracle.getAssetPrice(address(token));
  1152  
  1153          // @dev aave returns from same source as chainlink (which is 8 decimals)
  1154:         uint256 quotePrice = state.oracle.getAssetPrice(address(state.usdc));
  1155  

dn-gmx-vaults\contracts\vaults\DnGmxSeniorVault.sol:
  314      function getPriceX128() public view returns (uint256) {
  315:         uint256 price = oracle.getAssetPrice(address(asset));
  316  

  325          // use aave's oracle to get price of usdc
  326:         uint256 price = oracle.getAssetPrice(address(asset));
  327  
```

we are calling oracle.getAssetPrice(address(asset)) from Aave, which calls:

https://github.com/aave/aave-v3-core/blob/f3e037b3638e3b7c98f0c09c56c5efde54f7c5d2/contracts/misc/AaveOracle.sol#L104

```solidity
  /// @inheritdoc IPriceOracleGetter
  function getAssetPrice(address asset) public view override returns (uint256) {
    AggregatorInterface source = assetsSources[asset];

    if (asset == BASE_CURRENCY) {
      return BASE_CURRENCY_UNIT;
    } else if (address(source) == address(0)) {
      return _fallbackOracle.getAssetPrice(asset);
    } else {
      int256 price = source.latestAnswer();
      if (price > 0) {
        return uint256(price);
      } else {
        return _fallbackOracle.getAssetPrice(asset);
      }
    }
  }
```

note the line: 

```solidity
int256 price = source.latestAnswer();
```

the Aave's code does not check if the price get from the data is the updated price.

there is no freshness check. This could lead to stale prices being used.

If the market price of the token drops very quickly ("flash crashes"), and Chainlink's feed does not get updated in time, the smart contract will continue to believe the token is worth more than the market value.

Chainlink also advise developers to check for the updatedAt before using the price:

> Your application should track the latestTimestamp variable or use the updatedAt value from the latestRoundData() function to make sure that the latest answer is recent enough for your application to use it. If your application detects that the reported answer is not updated within the heartbeat or within time limits that you determine are acceptable for your application, pause operation or switch to an alternate operation mode while identifying the cause of the delay.

## Impact

A stale price can cause the malfunction of multiple features across the protocol:

```solidity
    function _getTokenPriceInUsdc(State storage state, IERC20Metadata token)
        private
        view
        returns (uint256 scaledPrice)
    {
        uint256 decimals = token.decimals();
        uint256 price = state.oracle.getAssetPrice(address(token));

        // @dev aave returns from same source as chainlink (which is 8 decimals)
        uint256 quotePrice = state.oracle.getAssetPrice(address(state.usdc));

        // token price / usdc price
        scaledPrice = price.mulDivDown(PRICE_PRECISION, quotePrice * 10**(decimals - 6));
    }
```

if the function getTokenPriceInUsdc used a stale price, the function that relies on this function to calculate the optimal threshold to borrow and calculate the optimal amount to flashloan are not accurate 

```solidity
    function _getOptimalCappedBorrows(
        State storage state,
        uint256 availableBorrowAmount,
        uint256 usdcLiquidationThreshold
    ) private view returns (uint256 optimalBtcBorrow, uint256 optimalEthBorrow) {
        // The value of max possible value of ETH+BTC borrow
        // calculated basis available borrow amount, liqudation threshold and target health factor
        // AAVE target health factor = (usdc supply value * usdc liquidation threshold)/borrow value
        // whatever tokens we borrow from AAVE (ETH/BTC) we sell for usdc and deposit that usdc into AAVE
        // assuming 0 slippage borrow value of tokens = usdc deposit value (this leads to very small variation in hf)
        // usdc supply value = usdc borrowed from senior tranche + borrow value
        // replacing usdc supply value formula above in AAVE target health factor formula
        // we can replace usdc borrowed from senior tranche with available borrow amount
        // we can derive max borrow value of tokens possible i.e. maxBorrowValue
        uint256 maxBorrowValue = availableBorrowAmount.mulDivDown(
            usdcLiquidationThreshold,
            state.targetHealthFactor - usdcLiquidationThreshold
        );

        // calculate the borrow value of eth & btc using their weights
        uint256 btcWeight = state.gmxVault.tokenWeights(address(state.wbtc));
        uint256 ethWeight = state.gmxVault.tokenWeights(address(state.weth));

        // get eth and btc price in usdc
        uint256 btcPrice = _getTokenPriceInUsdc(state, state.wbtc);
        uint256 ethPrice = _getTokenPriceInUsdc(state, state.weth);
```

## Code Snippet

https://github.com/sherlock-audit/2022-10-rage-trade/blob/main/dn-gmx-vaults/contracts/libraries/DnGmxJuniorVaultManager.sol#L1086-L1108

https://github.com/sherlock-audit/2022-10-rage-trade/blob/main/dn-gmx-vaults/contracts/libraries/DnGmxJuniorVaultManager.sol#L1128-L1156

https://github.com/sherlock-audit/2022-10-rage-trade/blob/main/dn-gmx-vaults/contracts/libraries/DnGmxJuniorVaultManager.sol#L1253-L1279

https://github.com/sherlock-audit/2022-10-rage-trade/blob/main/dn-gmx-vaults/contracts/libraries/DnGmxJuniorVaultManager.sol#L255-L269

## Tool used

Manual Review

## Recommendation

We recommend the project fetch the price from chainlink by calling the function latestRoundData() and add the freshness check.

```solidity
function latestRoundData() external view
    returns (
        uint80 roundId,
        int256 answer,
        uint256 startedAt,
        uint256 updatedAt,
        uint80 answeredInRound
    )
```


