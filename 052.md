ctf_sec

medium

# RebalanceHedge can revert if there is underflow in DnGmxSeniorVault.sol#availableBorrow, blocking withdraw and deposit in JuniorVault

## Summary

rebalanceHedge can revert if there is underflow in DnGmxSeniorVault.sol#availableBorrow, blocking withdraw and deposit in JuniorVault 

## Vulnerability Detail

We have to agree that state.rebalanceHedge is crucial because the beforeWith and afterDeposit hook in DnGmxJuniorVault.sol call this function.

```solidity
function beforeWithdraw(
	uint256 assets,
	uint256,
	address
) internal override {
	(uint256 currentBtc, uint256 currentEth) = state.getCurrentBorrows();

	//rebalance of hedge based on assets after withdraw (before withdraw assets - withdrawn assets)
	state.rebalanceHedge(currentBtc, currentEth, totalAssets() - assets, false);
}

function afterDeposit(
	uint256,
	uint256,
	address
) internal override {
	if (totalAssets() > state.depositCap) revert DepositCapExceeded();
	(uint256 currentBtc, uint256 currentEth) = state.getCurrentBorrows();

	//rebalance of hedge based on assets after deposit (after deposit assets)
	state.rebalanceHedge(currentBtc, currentEth, totalAssets(), false);
}
```

the implementation of the state.rebalanceHedge is long, but we need to focus on this section:

```solidity
// Settle net change in market value and deposit/withdraw collateral tokens
// Vault market value is just the collateral value since profit has been settled
// AAVE target health factor = (usdc supply value * usdc liquidation threshold)/borrow value
// whatever tokens we borrow from AAVE (ETH/BTC) we sell for usdc and deposit that usdc into AAVE
// assuming 0 slippage borrow value of tokens = usdc deposit value (this leads to very small variation in hf)
// usdc supply value = usdc borrowed from senior tranche + borrow value
// replacing usdc supply value formula above in AAVE target health factor formula
// we can derive usdc amount to borrow from senior tranche i.e. targetDnGmxSeniorVaultAmount
uint256 targetDnGmxSeniorVaultAmount = (state.targetHealthFactor - usdcLiquidationThreshold).mulDivDown(
optimalBorrowValue,
usdcLiquidationThreshold
);

// current usdc borrowed from senior tranche
uint256 currentDnGmxSeniorVaultAmount = _getUsdcBorrowed(state);

if (targetDnGmxSeniorVaultAmount > currentDnGmxSeniorVaultAmount) {
// case where we need to borrow more usdc
// To get more usdc from senior tranche, so usdc is borrowed first and then hedge is updated on AAVE
{
	uint256 amountToBorrow = targetDnGmxSeniorVaultAmount - currentDnGmxSeniorVaultAmount;
	uint256 availableBorrow = state.dnGmxSeniorVault.availableBorrow(address(this));
	if (amountToBorrow > availableBorrow) {
		// if amount to borrow > available borrow amount
		// we won't be able to hedge glp completely
		// convert some glp into usdc to keep the vault delta neutral
		// hedge the btc/eth of remaining amount
		uint256 optimalUncappedEthBorrow = optimalEthBorrow;

		// optimal btc and eth borrows basis the hedged part of glp
		(optimalBtcBorrow, optimalEthBorrow) = _getOptimalCappedBorrows(
			state,
			currentDnGmxSeniorVaultAmount + availableBorrow,
			usdcLiquidationThreshold
		);

		// rebalance the unhedged glp (increase/decrease basis the capped optimal token hedges)
		_rebalanceUnhedgedGlp(state, optimalUncappedEthBorrow, optimalEthBorrow);

		if (availableBorrow > 0) {
			// borrow whatever is available since required > available
			state.dnGmxSeniorVault.borrow(availableBorrow);
		}
	} else {
		//No unhedged glp remaining so just pass same value in capped and uncapped (should convert back any ausdc back to sglp)
		_rebalanceUnhedgedGlp(state, optimalEthBorrow, optimalEthBorrow);

		// Take from LB Vault
		state.dnGmxSeniorVault.borrow(targetDnGmxSeniorVaultAmount - currentDnGmxSeniorVaultAmount);
	}
}
```

note the line:

```solidity
state.dnGmxSeniorVault.borrow(availableBorrow);
```

and

```solidity
 state.dnGmxSeniorVault.borrow(targetDnGmxSeniorVaultAmount - currentDnGmxSeniorVaultAmount);
```

We are calling DnGmxSeniorVault.sol#borrow:

```solidity
function borrow(uint256 amount) external onlyBorrower {
  // revert on invalid borrow amount
  if (amount == 0 || amount > availableBorrow(msg.sender)) revert InvalidBorrowAmount();

  // lazily harvest fees (harvest would return early if not enough rewards accrued)
  dnGmxJuniorVault.harvestFees();

  // transfers aUsdc to borrower
  // but doesn't reduce totalAssets of vault since borrwed amounts are factored in
  aUsdc.transfer(msg.sender, amount);
}
```

note this line:

```solidity
if (amount == 0 || amount > availableBorrow(msg.sender)) revert InvalidBorrowAmount();
```

which calls:

```solidity
function availableBorrow(address borrower) public view returns (uint256 availableAUsdc) {
	uint256 availableBasisCap = borrowCaps[borrower] - IBorrower(borrower).getUsdcBorrowed();
	uint256 availableBasisBalance = aUsdc.balanceOf(address(this));

	availableAUsdc = availableBasisCap < availableBasisBalance ? availableBasisCap : availableBasisBalance;
}
```

what is IBorrower(borrower).getUsdcBorrowed()?

```solidity
/// @notice returns total amount of usdc borrowed from senior vault
/// @dev all aUSDC yield from AAVE goes to the senior vault
/// @dev deducts junior vault usdc (swapped + unhedged glp) from overall balance
/// @return usdcAmount borrowed from senior tranche
function getUsdcBorrowed() public view returns (uint256 usdcAmount) {
	return
		uint256(
			state.aUsdc.balanceOf(address(this)).toInt256() -
				state.dnUsdcDeposited -
				state.unhedgedGlpInUsdc.toInt256()
		);
}
```

We have all the information to reason about this vulnerability:

note this line:

```solidity
uint256 availableBasisCap = borrowCaps[borrower] - IBorrower(borrower).getUsdcBorrowed();
```

if IBorrower(borrower).getUsdcBorrowed() > borrowCaps[borrower], this line of code strictly revert in underflow, which DnGmxSeniorVault.sol#borrow, which revert rebalanceHedge, which deposit and withdraw in DnGmxJuniorVault.sol and all rebalanceHedge operation.

if we look back to the getUsdcBorrowed function:

```solidity
function getUsdcBorrowed() public view returns (uint256 usdcAmount) {
	return
		uint256(
			state.aUsdc.balanceOf(address(this)).toInt256() -
				state.dnUsdcDeposited -
				state.unhedgedGlpInUsdc.toInt256()
		);
}
```

the hacker can just send the aUSDC token to the junior vault address state.aUsdc.balanceOf(address(this)).toInt256(),

or  if there is a sudden decrease of state.dnUsdcDeposited and state.unhedgedGlpInUsdc, this all increase the value of getUsdcBorrowed() and make this line of code revert in underflow.

```solidity
uint256 availableBasisCap = borrowCaps[borrower] - IBorrower(borrower).getUsdcBorrowed();
```

## Impact

rebalanceHedge can revert there is underflow in DnGmxSeniorVault.sol#availableBorrow

## Code Snippet

## Code Snippet

https://github.com/sherlock-audit/2022-10-rage-trade/blob/main/dn-gmx-vaults/contracts/vaults/DnGmxJuniorVault.sol#L707-L729

https://github.com/sherlock-audit/2022-10-rage-trade/blob/main/dn-gmx-vaults/contracts/vaults/DnGmxSeniorVault.sol#L155-L168

https://github.com/sherlock-audit/2022-10-rage-trade/blob/main/dn-gmx-vaults/contracts/vaults/DnGmxSeniorVault.sol#L183-L195

https://github.com/sherlock-audit/2022-10-rage-trade/blob/main/dn-gmx-vaults/contracts/vaults/DnGmxSeniorVault.sol#L349-L356

https://github.com/sherlock-audit/2022-10-rage-trade/blob/main/dn-gmx-vaults/contracts/libraries/DnGmxJuniorVaultManager.sol#L571-L607

## Tool used

Manual Review

## Recommendation

We recommend the project monitor the borrowCaps[borrower] and call function updateBorrowCap to update the borrow cap as the owner fit.

Also, instead of the letting the transaction revert in this two line:

```solidity
if (amount == 0 || amount > availableBorrow(msg.sender)) revert InvalidBorrowAmount();
```

and

```solidity
uint256 availableBasisCap = borrowCaps[borrower] - IBorrower(borrower).getUsdcBorrowed();
```

we need to add the safety before reaching these line:

We can change from

```solidity
uint256 availableBasisCap = borrowCaps[borrower] - IBorrower(borrower).getUsdcBorrowed();
```

to

```solidity
 if(IBorrower(borrower).getUsdcBorrowed() > borrowCaps[borrower]) {
   return 0
 }
```

or if(IBorrower(borrower).getUsdcBorrowed() > borrowCaps[borrower]), pause the contract and wait for the admin to update the borrow cap.

If we know

```solidity
if (amount == 0 || amount > availableBorrow(msg.sender)) revert InvalidBorrowAmount();
```

do not let the transaction revert, handle the transaction before to not let the borrow affecting the rebalanceHedge.
